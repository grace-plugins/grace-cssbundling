plugins {
    id "eclipse"
    id "idea"
    id "groovy"
    id "org.graceframework.grace-plugin"
    id "maven-publish"
    id "signing"
}

version projectVersion
group "org.graceframework.plugins"

repositories {
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {
    compileOnly "org.graceframework:grace-core"
    compileOnly "org.graceframework:grace-plugin-api"
    profile "org.graceframework.profiles:plugin"
}

tasks.withType(Sign) {
   onlyIf { isReleaseVersion }
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.mainAttributes(
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": "Grace CSS Bundling Plugin",
        "Implementation-Version": projectVersion,
        "Implementation-Vendor": 'Grace Plugins')
    enabled = true
    includeEmptyDirs = false
}

bootJar.enabled = false
bootRun.enabled = false
findMainClass.enabled = false
shell.enabled = false

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            from components.java

            artifact source: "${project.sourceSets.main.groovy.destinationDirectory.get()}/META-INF/grails-plugin.xml",
                classifier: "plugin",
                extension: 'xml'
            pom {
                name = "Grace CSS Bundling Plugin"
                description = "Bundle and process CSS in Grace with Tailwind, PostCSS, and Sass via Node.js."
                url = 'https://github.com/grace-plugins/grace-cssbundling'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rainboyan'
                        name = 'Michael Yan'
                        email = 'rain@rainboyan.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/grace-plugins/grace-cssbundling.git'
                    developerConnection = 'scm:git:ssh://github.com:grace-plugins/grace-cssbundling.git'
                    url = 'https://github.com/grace-plugins/grace-cssbundling'
                }
            }

            pom.withXml {
                def pomNode = asNode()

                try {
                    pomNode.dependencyManagement.replaceNode {}
                } catch (Throwable e) {
                    // ignore
                }

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    try {
                        it.replaceNode {}
                    } catch (Throwable e) {
                        // ignore
                    }
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}
